def _scripts = System.getenv('CG_CM_SCRIPTS_HOME') ?: 'D:/CG/src/cg-cm/cg-cm-scripts'
ext.scripts = project.hasProperty('superpom') ? project.superpom : "$_scripts/gradlebuild"
logger.info "Calculated 'superpom' location: $scripts"

File f = null
try {
  f = file("$scripts/repos.gradle")
} catch (e) {}

ext.libs = [
	'jackson': 'com.fasterxml.jackson.core:jackson-databind:2.6.0',
	'osito': 'org.osito:osito-javafx:0.0.1-SNAPSHOT'
]

allprojects {
	group = 'ee.elastic'
	version = '1.0-SNAPSHOT'
	defaultTasks 'build','install'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

	sourceCompatibility = 1.7

	[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

	dependencies {
		testCompile 'junit:junit:4.10'
	}

	if(f && f.exists()) {
		println "Use repos from $f.absolutePath"
		apply from: f.absolutePath
	  } else {
		println "Use default repos"
		repositories {
			mavenLocal()
			mavenCentral()
			maven {	url "https://oss.sonatype.org/content/repositories/releases" }
			maven { url "http://mirrors.ibiblio.org/maven2" }
		}
	}

	jar {
    	manifest.attributes provider: 'EE Software'
	}

	task cleanAll << { task ->
		['bin','target','build','dist','temp','dist.zip'].each { name ->
			def file = new File("$task.project.name/$name")
			if(file.exists()) if(file.isDirectory())file.deleteDir() else file.delete()
		}
	}
}
